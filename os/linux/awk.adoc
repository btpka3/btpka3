

* link:https://www.gnu.org/s/gawk/manual/gawk.html[The GNU Awk User’s Guide]
* link:https://stackoverflow.com/questions/43250592/awk-to-read-file-as-a-whole[Awk to read file as a whole]

awk 有不同的实现，如果下面示例不可用，请尝试使用 gawk （GNU版本的）

## xxx

[source,shell]
----
# 多个分隔符， -F 使用的 正则表达是
echo "aaa|bbb,ccc" | awk -F"[\|,]" '{print   $1 "___" $2 "___" $3 }'
echo "aaa|bbb,ccc" | gawk -F"[|,]" '{printf("%s___%s___%s", $1, $2, $3)}'

echo "aaallbbbllccc" | gawk -F"ll" '{printf("%s___%s___%s", $1, $2, $3)}'
echo "aaal[bbbl[ccc" | gawk -F'l\\[' '{printf("%s___%s___%s", $1, $2, $3)}'

# MacOS 上这样安装
brew install gawk

# 字符串拼接
echo aaa | awk -v x=xxx '{ v="11" $0 "22" x "33"; print v }'

ll | awk '{print $5}'

awk -F"|" '{print $5}'

# 添加前缀

cat << EOF | awk -v prefix=xxx. '$0=prefix $0'
aaa
bbb
ccc
EOF

# 输出最后一个field
awk -F" " '{print $NF}'

# 输出每个字段
## $1=$1 # Re-arranging first field to make OFS into picture, since by default OFS is space and I am setting it as a new line.
##       # 当将 $1 重新赋值给 $1 后，awk 会 将所有字段重新使用 OFS 连接起来, 而无参的 print 会输出新的 $0
##       # https://www.gnu.org/software/gawk/manual/html_node/Changing-Fields.html
cat <<EOF |
aaa,bbb,ccc
xxx,yyy,zzz
111,222,333
EOF
awk '
BEGIN{FS=",";OFS="\n"}
{$1=$1;print;}
'

# 替换分隔符
cat <<EOF |
aaa|bbb|ccc
xxx|yyy|zzz
111|222|333
EOF
awk '
BEGIN{FS="|";OFS="\t"}
{$1=$1;print;}
' | xargs -I {} echo "=== {}"

# 字符串替换
# 删除999
cat <<EOF |
999aaa999|bbb|ccc
999xxx999|yyy|zzz
999111999|222|333
EOF
gawk '
BEGIN{FS="|";OFS="|"}
{$1=gensub(/999/,"","g",$1);print;}
' | xargs -I {} echo "=== {}"



# 输出每个字段
IN="aaa;bbb;ccc"
for i in $(echo $IN | tr ";" "\n")
do
  echo $i
done


# 替换换行符 "\r\n" -> "\n"
awk 'BEGIN{RS="\r\n"; ORS="\n"}{print $0}' file > file

# 行内求和
echo 1 1 2 300 | awk '{sum=0;i=1; while(i<5){ sum+=$i; i++} avg=sum/4; print "avg/sum = ",avg,sum}'

# 多行求和
ll | awk '{s+=$5} END{print s}'

# 条件过滤
ll | awk '{ if($5>10000) print $0}'

echo "aaa bbb" | gawk '{
  if(match($0, /(.*) (.*)/, arr)){
     print arr[1] "=" arr[2]
  }
}'

# 正则提取分组
echo '"Common Cleaner" Id=21 BLOCKED on AAA@111 owned by "AAA Handler" Id=11' |
gawk '
{
    if(match($0, /^"[^"]*" [^ ]+ \w+ on ([^ ]+) owned by "[^"]*" Id=([^ ]+)$/, arr)){
        print arr[2] " " arr[1]
    }
}
'

# 作为引号中的命令
echo 'ls -l | awk -F"|" '"'"'{ if($5>10000) print $0}'"'"

# group，count
cat << EOF | awk -F, '{array[$1]++} END{for(k in array){print k,array[k] | "sort -k1"}}'
Item1,200
Item1,200
Item3,900
Item2,500
Item2,800
Item1,600
Item4,
Item5,
Item4,100
Item5,
Item5,444
EOF


# group，distinct concat
cat << EOF |
X1,AAA,101
X2,AAA,102
X2,AAA,102
X3,CCC,301
X4,BBB,201
X5,BBB,202
X6,AAA,103
EOF
awk -F, '{print $2, $3}' | sort | uniq |
awk '
{
    if(array[$1] == "") {
        array[$1] = $2 ;
    } else {
        array[$1] = array[$1] "," $2
    }
}
END {
    for( k in array ){
     print k " " array[k]
    }
}
' | sort



# group，sum
cat << EOF |
time01,AAA,1
time01,AAA,2
time01,BBB,3
time02,AAA,100
time02,BBB,200
time02,BBB,300
EOF
gawk -F, '
BEGIN{FS=",";OFS="|"}
{array[$1][$2] += $3}
END {
    for(time in array ) {
        for( type in array[time] ) {
            print time OFS type OFS array[time][type]
        }
    }
}
'
----



### float -> int

[source,shell]
----
printf "%4.3e\n", 1950
printf "%.0f\n", 3.1415926
printf "%.3f\n", 3.1415926

grep 'cpu ' /proc/stat | awk '{usage=($2+$4)*100/($2+$4+$5)} END {printf "%.1f" , usage}'
----

### 行内统计

[source,shell]
----
# 统计行内 数值字符串 是个数，并输出

cat <<EOF |
aa 11 bb
bb
33 ee ff 88
cc 99 00 66 dd
EOF
awk '
{
    delete numArr;
    numArrStr = "" ;
    numberCount=0
    s=$0
    while (match(s, /([[:digit:]]+)/, sArr)) {
        numberCount++ ;
        numArr[numberCount]=sArr[1];

        s=substr(s, sArr[1, "start"]+sArr[1, "length"]);
    }
    for(i in numArr){
        if( i==1){
            numArrStr = numArr[i];
        } else {
            numArrStr = numArrStr "," numArr[i]
        }
    }
    printf "%-30s : %2d : %s \n", $0 , numberCount, numArrStr;
}
'
----


### 提取字符串

link:https://www.gnu.org/software/gawk/manual/html_node/Bracket-Expressions.html#Bracket-Expressions[Using Bracket Expressions]

[source,shell]
----
cat <<EOF |
中华1A18912XDFUDTTSLVP48EC人民
  1A18927UAEB74UQOOW0AUD
22 1A18912XDFUDTTSLVP48EC 1A18912YD4KDTTN4NEHKZ
O ddd
EOF
awk --re-interval '
{
    s=$0
    while(match(s, /([A-Z0-9]{22})/, sArr)) {
        printf "%s\n", sArr[1];
        s=substr(s, sArr[1, "start"]+sArr[1, "length"]);
    }
}
'
----


### 截取

[source,shell]
----

cat <<EOF |
2020-03-20 14:48:41,166 [http-bio-7001-exec-15] WARN  c.a.s.t.a.c.a.UserDetailServiceImpl - user not found : 111
2020-03-20 14:50:14,165 [http-bio-7001-exec-19] WARN  c.a.s.t.a.c.a.UserDetailServiceImpl - user not found : 222
2020-03-20 14:51:48,487 [http-bio-7001-exec-4] WARN  c.a.s.t.a.c.a.UserDetailServiceImpl - user not found : 222
2020-03-20 14:53:22,138 [http-bio-7001-exec-61] WARN  c.a.s.t.a.c.a.UserDetailServiceImpl - user not found : 333
EOF
awk 'match($0, /.*user not found : (.*)/, arr) {print arr[1]}' | sort | uniq
----


### 替换
替换前N行中满足条件的数据

[source,shell]
----
cat <<EOF |
   <groupId>aa.bb.cc</groupId>
   <version>1.1.1</version>
   <properties>
      <slf4j.version>1.7.2<slf4j.version>
      <version>2.2.2</version>
   </properties>
EOF
awk -v newVersion=3.3.3  '
{
    if(FNR<4){
      gsub("<version>.*</version>", ("<version>" newVersion "</version>"))
    }
    print
}
'
----

### trim

[source,shell]
----
echo  "Main-Class: org.springframework.boot   .loader.JarLauncher " | awk -F':' '{gsub(/[ \t\n]+/, "", $2); print "==" $2 "=="}'
----

### 多行分割

[source,shell]
----
cat <<EOF |
class-info        com.taobao.eagleeye.EagleEye
code-source       /tmp/bbb.jar
classLoaderHash   69454fd4

class-info        com.taobao.eagleeye.EagleEye
code-source       /taobao-hsf.sar/aaa.jar
classLoaderHash   1b5b6fcb

class-info        com.taobao.eagleeye.EagleEye
code-source       /tmp/ccc.jar
classLoaderHash   68f952fe
EOF
gawk  ' BEGIN{RS="class-info"; ORS=""} {  if(length($0) !=0){ print "========\n" RS $0}}'
----


### 替换-多行

[source,shell]
----
cat <<EOF |
<project>
  <parent>
    <groupId>aa.bb.cc</groupId>
<<<<<<< HEAD
    <version>0.7.87-changletest-SNAPSHOT</version>
=======
    <version>0.7.81-SNAPSHOT</version>
>>>>>>> 34b96fa2474550722791e84d3749dbf6f50504db
    <relativePath>../pom.xml</relativePath>
  </parent>
  <artifactId>xxx-api</artifactId>
  <properties>
    <slf4j.version>1.7.2<slf4j.version>
    <version>2.2.2</version>
  </properties>
</project>
EOF
gawk -v newVersion=3.3.3  '
{
    print gensub("<<<<<<< .{1,40}\n(\\s*)<version>[^<>]*</version>\n=======\n\\s*<version>[^<>]*</version>\n>>>>>>> .{1,40}\n", "\\1<version>" newVersion "</version>\n", "g")

}
' RS='^$'
----




### 查找过滤-多行
link:https://www.gnu.org/software/gawk/manual/html_node/gawk-split-records.html[Record Splitting with gawk]

[source,shell]
----
# 过滤出相关内容
# 注意RS中使用 '^', '$' 时，总是匹配整个文件内容的开始和结尾。
# 由于 awk 中不支持 non-capturing group, 故输出时，需要手动将处理

cat <<EOF |
"aaa" "a111"
   a1
   a2
   a3
"bbb" "b111"
   b1
   b2
   b3
"ccc" "c111"
   c1
   c2
   c3
EOF
gawk  '
BEGIN{RS="\n\""; ORS=""}
{
 if (match($0, "bbb|ccc")){
   print  "========\n" (FNR == 1 ? "" : "\"") $0 "\n" }
 }
'
----


### 行统计-多行

[source,shell]
----
# 过滤出相关内容,并按照行数去统计排序，找出TOP-n
cat <<EOF |
"aaa" "a111"
   line-101
   line-201
   line-301
"bbb" "b111"
   line-301
   line-401
"ccc" "c111"
   line-301
   line-201
   line-501
EOF
gawk  '
BEGIN{RS="\n\""; ORS="";FS="\n"}
{
  if (match($0, "bbb|ccc")){
    print (FNR == 1 ? "" : "\"") $0 "\n"
  }
}
' | grep . | grep -E -v "^\"" | sort | uniq -c | sort -k1 -n -r
----


[source,shell]
----
# 过滤出相关内容,并按照行数去统计排序，找出TOP-n
cat <<EOF |
ts="111" "1111"
   aaa
   aaa
   aaa
ts="222" "2222"
   bbb
   bbb
   bbb
ts="333" "3333"
   bbb
   bbb
   bbb
ts="444" "4444"
   aaa
   aaa
   aaa
ts="555" "5555"
   ccc
   ccc
ts="666" "666"
   aaa
   aaa
   aaa


EOF
gawk '
BEGIN{RS="\nts=";ORS="";}
{
  # 移除首行，只保留堆栈
  stack = $0;
  n = index(stack, "\n")
  if(n > 0){
    stack=substr(stack, n+1 )
  }
  # 移除行尾空白
  stack=gensub(/\s+$/, "", "g", stack)
  arr[stack] ++
}
END {
  PROCINFO["sorted_in"] = "@val_num_desc";
  for (stack in arr){
    print  "====== : " arr[stack] "\n" stack "\n"
  }
}
'




grep -v -F "Command execution times exceed limit" /tmp/a.txt | \
grep -v -F "Affect(class count:" | \
awk '
BEGIN{RS="\nts=";ORS="";}
{
  # 移除首行，只保留堆栈
  stack = $0;
  n = index(stack, "\n")
  if(n > 0){
    stack=substr(stack, n+1 )
  }
  # 移除行尾空白
  stack=gensub(/\s+$/, "", "g", stack)
  arr[stack] ++
}
END {
  PROCINFO["sorted_in"] = "@val_num_desc";
  for (stack in arr){
    print  "====== : " arr[stack] "\n" stack "\n"
  }
}
'

/tmp/a.txt | head -n 50 |
print  "====== : [" n "] :\n" stack "\n"
----








[source,shell]
----
CONTENT='
"AAA Handler" Id=11 RUNNABLE
        at java.base@11.0.15.14-JDK/java.lang.ref.Reference.waitForReferencePendingList(Native Method)
        at java.base@11.0.15.14-JDK/java.lang.ref.Reference.processPendingReferences(Reference.java:241)

        Number of locked synchronizers = 1
        - java.util.concurrent.ThreadPoolExecutor$Worker@534c8c29

"Common-Cleaner" Id=21 BLOCKED on AAA@111 owned by "AAA Handler" Id=11
        at java.base@11.0.15.14-JDK/java.lang.Object.wait(Native Method)
        -  waiting on AAA@111
        at java.base@11.0.15.14-JDK/java.lang.ref.ReferenceQueue.remove(ReferenceQueue.java:155)

"AAA Handler" Id=12 RUNNABLE
        at java.base@11.0.15.14-JDK/java.lang.ref.Reference.waitForReferencePendingList(Native Method)
        at java.base@11.0.15.14-JDK/java.lang.ref.Reference.processPendingReferences(Reference.java:241)

        Number of locked synchronizers = 1
        - java.util.concurrent.ThreadPoolExecutor$Worker@534c8c29

"AAA Handler" Id=13 RUNNABLE
        at java.base@11.0.15.14-JDK/java.lang.ref.Reference.waitForReferencePendingList(Native Method)
        at java.base@11.0.15.14-JDK/java.lang.ref.Reference.processPendingReferences(Reference.java:241)

        Number of locked synchronizers = 1
        - java.util.concurrent.ThreadPoolExecutor$Worker@534c8c29

"Common-Cleaner" Id=22 BLOCKED on AAA@222 owned by "AAA Handler" Id=11
        at java.base@11.0.15.14-JDK/java.lang.Object.wait(Native Method)
        -  waiting on AAA@222
        at java.base@11.0.15.14-JDK/java.lang.ref.ReferenceQueue.remove(ReferenceQueue.java:155)

"Common-Cleaner" Id=23 BLOCKED on BBB@111 owned by "AAA Handler" Id=14
        at java.base@11.0.15.14-JDK/java.lang.Object.wait(Native Method)
        -  waiting on BBB@111
        at java.base@11.0.15.14-JDK/java.lang.ref.ReferenceQueue.remove(ReferenceQueue.java:155)

"Common-Cleaner" Id=24 BLOCKED on BBB@111 owned by "AAA Handler" Id=14
        at java.base@11.0.15.14-JDK/java.lang.Object.wait(Native Method)
        -  waiting on BBB@111
        at java.base@11.0.15.14-JDK/java.lang.ref.ReferenceQueue.remove(ReferenceQueue.java:155)

"AAA Handler" Id=14 RUNNABLE
        at java.base@11.0.15.14-JDK/java.lang.ref.Reference.waitForReferencePendingList(Native Method)
        at java.base@11.0.15.14-JDK/java.lang.ref.Reference.processPendingReferences(Reference.java:241)

        Number of locked synchronizers = 1
        - java.util.concurrent.ThreadPoolExecutor$Worker@534c8c29
'
STACK_FILE=/tmp/stack.log.2023-02-03_23:30:40
echo $CONTENT > $STACK_FILE

cat $STACK_FILE | grep BLOCKED | gawk '
{
    if(match($0, /^"[^"]*" [^ ]+ \w+ on ([^ ]+) owned by "[^"]*" Id=([^ ]+)$/, arr)){
        lock     = arr[1]
        threadId = arr[2]
        print threadId " " lock
    }
}
' | sort | uniq |
gawk '
{
    if(match($0, /(.*) (.*)/, arr)){
        threadId=arr[1]
        lock=arr[2]
        if(array[threadId] == "") {
            array[threadId] = lock ;
        } else {
            array[threadId] = array[threadId] "," lock
        }
    }
}
END {
    for( threadId in array ){
       print threadId " " array[threadId]
    }
}
' |
xargs -I '{}' gawk -v id_lock={} -v hostname=`hostname` -v STACK_FILE=$STACK_FILE '
BEGIN {
    if(match(id_lock, /(.*) (.*)/, arr)){
        threadId=arr[1]
        lock=arr[2]
    }
}
{
   s=$0
   regex = "(\"[^\"]*\" Id=" threadId " RUNNABLE)\n([^\"]*(\n\n|$))"
   while(match(s, regex, arr)){
      print "==================== " hostname ":" STACK_FILE;
      print arr[1] " lock " lock;
      print arr[2];
      s = substr(s, arr[1, "start"] + arr[1, "length"]);
   }
}
' RS='^$' $STACK_FILE
----


[source,shell]
----
for STACK_FILE in /home/admin/logs/hsf/HSF_JStack.log.2023-02-09_10:38:31
do
    grep BLOCKED $STACK_FILE | awk '
{
    if(match($0, /^"[^"]*" [^ ]+ \w+ on ([^ ]+) owned by "[^"]*" Id=([^ ]+)$/, arr)){
        lock     = arr[1]
        threadId = arr[2]
        print threadId " " lock
    }
}
' | sort | uniq |
awk '
{
    if(match($0, /(.*) (.*)/, arr)){
        threadId=arr[1]
        lock=arr[2]
        if(array[threadId] == "") {
            array[threadId] = lock ;
        } else {
            array[threadId] = array[threadId] "," lock
        }
    }
}
END {
    for( threadId in array ){
       print threadId " " array[threadId]
    }
}
' |
xargs -I '{}' gawk -v id_lock={} -v hostname=`hostname` -v STACK_FILE="${STACK_FILE}" '
BEGIN {
    if(match(id_lock, /(.*) (.*)/, arr)){
        threadId=arr[1]
        lock=arr[2]
    }
}
{
   s=$0
   regex = "(\"[^\"]*\" Id=" threadId " RUNNABLE)\n([^\"]*(\n\n|$))"
   while(match(s, regex, arr)){
      print "==================== " hostname ":" STACK_FILE;
      print arr[1] " lock " lock;
      print arr[2];
      s = substr(s, arr[1, "start"] + arr[1, "length"]);
   }
}
' RS='^$' $STACK_FILE
done
----


[source,shell]
----
LOG_FILE=HSF_JStack_stat_`date +%Y%m%d%H%M%S`.log

pgm -b -p 20 `armory -leg mtee3.content_sync.prodhost` '
for STACK_FILE in /home/admin/logs/hsf/HSF_JStack.log*
do
    grep BLOCKED $STACK_FILE | awk '"'"'
{
    if(match($0, /^"[^"]*" [^ ]+ \w+ on ([^ ]+) owned by "[^"]*" Id=([^ ]+)$/, arr)){
        lock     = arr[1]
        threadId = arr[2]
        print threadId " " lock
    }
}
'"'"' | sort | uniq |
awk '"'"'
{
    if(match($0, /(.*) (.*)/, arr)){
        threadId=arr[1]
        lock=arr[2]
        if(array[threadId] == "") {
            array[threadId] = lock ;
        } else {
            array[threadId] = array[threadId] "," lock
        }
    }
}
END {
    for( threadId in array ){
       print threadId " " array[threadId]
    }
}
'"'"' |
xargs -I {} gawk -v id_lock={} -v hostname=`hostname` -v STACK_FILE="${STACK_FILE}" '"'"'
BEGIN {
    if(match(id_lock, /(.*) (.*)/, arr)){
        threadId=arr[1]
        lock=arr[2]
    }
}
{
   s=$0
   regex = "(\"[^\"]*\" Id=" threadId " RUNNABLE)\n([^\"]*(\n\n|$))"
   while(match(s, regex, arr)){
      print "==================== " hostname ":" STACK_FILE;
      print arr[1] " lock " lock;
      print arr[2];
      s = substr(s, arr[1, "start"] + arr[1, "length"]);
   }
}
'"'"' RS='"'"'^$'"'"' $STACK_FILE
done
' > $LOG_FILE

zip -r ${LOG_FILE}.zip $LOG_FILE
----


## regex

### 转义字符
[source,plain]
----
\\          # 匹配单个字符 `\`
\a          # The “alert” character, Ctrl-g, ASCII code 7 (BEL)
\b          # Backspace, Ctrl-h, ASCII code 8 (BS).
\f          # Formfeed, Ctrl-l, ASCII code 12 (FF).
\n          # Newline, Ctrl-j, ASCII code 10 (LF).
\r          # Carriage return, Ctrl-m, ASCII code 13 (CR).
\t          # Horizontal TAB, Ctrl-i, ASCII code 9 (HT).
\v          # Vertical TAB, Ctrl-k, ASCII code 11 (VT).
\nnn        # The octal value nnn, where nnn stands for 1 to 3 digits between ‘0’ and ‘7’.
            # For example, the code for the ASCII ESC (escape) character is ‘\033’.
\xhh…       # `hh` 表示十六进制的字符 [0-9a-fA-F], `\x` 之后最多允许2个字符
\uhh…       # `hh` 表示十六进制的字符 [0-9a-fA-F], `\x` 之后最多允许8个字符
\/          # 匹配单个字符 `/`
\"          # 匹配单个字符 `"`
----

### 正则表达式操作符
[source,plain]
----
\           # 用来转义特殊字符串，比如  `\$` 匹配单个字符串 `$`, 而不是表示匹配行尾
^           # 匹配行首
$           # 匹配行尾
.           # 匹配任意单个字符
[…]         # 匹配反括号内的任意单个字符。
[^…]        # 不匹配方括号内的任意字符
|           # 或
(…)         # 分组
*           # 出现0次或任意多次
+           # 出现至少1次
?           # 出现至少0次，或1次
{n}         # 出现至少n次
{n,}        # 出现至少n次
{n,m}       # 出现至少n次，最多m次
----

### 特殊的方括号表达式
[source,plain]
----
[:alnum:]	Alphanumeric characters
[:alpha:]	Alphabetic characters
[:blank:]	Space and TAB characters
[:cntrl:]	Control characters
[:digit:]	Numeric characters
[:graph:]	Characters that are both printable and visible (a space is printable but not visible, whereas an ‘a’ is both)
[:lower:]	Lowercase alphabetic characters
[:print:]	Printable characters (characters that are not control characters)
[:punct:]	Punctuation characters (characters that are not letters, digits, control characters, or space characters)
[:space:]	Space characters (these are: space, TAB, newline, carriage return, formfeed and vertical tab)
[:upper:]	Uppercase alphabetic characters
[:xdigit:]	Characters that are hexadecimal digits
----
### gawk 专有转义操作符

[source,plain]
----
\s          # 匹配空白，等价于 `[[:space:]]`
\S          # 不匹配空白，等价于 `[^[:space:]]`
\w          # 等价于  `[[:alnum:]_]`
\W          # 等价于  `[^[:alnum:]_]`
\<          # 匹配一个词开头前的空字符串，比如 `/\<away/` 匹配 "away", 但不匹配 "stowaway"
\>          # 匹配一个词结尾后的空字符串，比如 `/stow\>/` 匹配 "stow", 但不匹配 "stowaway"
\y          # 匹配一个词开头前、或结尾后的空字符串，用来表示一个词的边界
\B          # 匹配词内匹配（不含开头与结尾）。比如 `/\Brat\B/` 匹配 "crate", 但不匹配 "dirty rat"
----

### 常用正则示例

[source,shell]
----
# 提取更则分组, 但只支持贪婪限定符（Greedy quantifiers），不支持 Reluctant quantifiers
echo aaa111ccc222ccc | gawk '{if(match($0, /aaa(.*)ccc/, arr)){ print arr[1] }}'

----



## 删除第一行

[source,shell]
----
# 命令拆解
cat <<EOF |
aaa
bbb
ccc
EOF
tee a.txt

awk 'NR>1' a.txt

# 上述命令合并
cat <<EOF |
aaa
bbb
ccc
EOF
awk 'NR>1'
----
